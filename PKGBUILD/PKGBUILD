# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>
# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kirikaze Chiyuki <me@chyk.ink>

pkgname=elisa-yuki-git
pkgver=f012f750
pkgrel=1
arch=(x86_64)
pkgdesc="Simple music player aiming to provide a nice experience for its users, forked by @chiyuki0325"
url="https://github.com/chiyuki0325/elisa-yuki"
license=(LGPL-3.0-or-later)
depends=(kcolorscheme kconfig kconfigwidgets kcoreaddons kcrash kdbusaddons kfilemetadata ki18n kiconthemes kio kirigami kitemviews kxmlgui qqc2-desktop-style qt6-base qt6-declarative qt6-multimedia)
makedepends=(extra-cmake-modules git kdoctools python)
provides=(elisa)
conflicts=(elisa)
source=("git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd elisa-yuki
  # git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  git --no-pager log -n 1 --pretty="%h"
}

build() {
  cmake -B build -S elisa-yuki \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package(){
  DESTDIR="$pkgdir" cmake --install build
}
